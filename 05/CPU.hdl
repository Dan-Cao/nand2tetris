// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    
    // Helper pin for a instruction
    Not(in=instruction[15], out=isAInstr);
    
    // Helper pins for c instruction
    And(a=instruction[15], b=true, out=isCInstr);
    And(a=instruction[12], b=true, out=a);
    And(a=instruction[11], b=true, out=c1);
    And(a=instruction[10], b=true, out=c2);
    And(a=instruction[9], b=true, out=c3);
    And(a=instruction[8], b=true, out=c4);
    And(a=instruction[7], b=true, out=c5);
    And(a=instruction[6], b=true, out=c6);
    And(a=instruction[5], b=true, out=d1);
    And(a=instruction[4], b=true, out=d2);
    And(a=instruction[3], b=true, out=d3);
    And(a=instruction[2], b=true, out=j1);
    And(a=instruction[1], b=true, out=j2);
    And(a=instruction[0], b=true, out=j3);
    
    // A register
    Mux16(a=aluOut, b=instruction, sel=isAInstr, out=registerAIn);
    Or(a=d1, b=isAInstr, out=loadA);
    ARegister(in=registerAIn, load=loadA, out=registerAOut);
    
    // D register
    And(a=d2, b=isCInstr, out=loadD);
    DRegister(in=aluOut, load=loadD, out=registerDOut);
    
    // ALU
    Mux16(a=registerAOut, b=inM, sel=a, out=aOrM);
    ALU(x=registerDOut, y=aOrM, zx=c1, nx=c2, zy=c3, ny=c4, f=c5, no=c6, out=aluOut, zr=aluZr, ng=aluNg);
        
    // Program counter
    And(a=aluNg, b=j1, out=jmp1);
    And(a=aluZr, b=j2, out=jmp2);
    
    Or(a=aluNg, b=aluZr, out=tmp1);
    Not(in=tmp1, out=tmp4); // Positive ALU output?
    And(a=tmp4, b=j3, out=jmp3);
    
    Or(a=jmp1, b=jmp2, out=tmp2);
    Or(a=tmp2, b=jmp3, out=tmp3);    
    And(a=tmp3, b=isCInstr, out=jump);
    PC(in=registerAOut, load=jump, inc=true, reset=reset, out[0..14]=pc);
    
    // Output pins
    And16(a=aluOut, b[0..15]=true, out=outM);
    And(a=d3, b=isCInstr, out=writeM);    
    And16(a=registerAOut, b=true, out[0..14]=addressM);
}