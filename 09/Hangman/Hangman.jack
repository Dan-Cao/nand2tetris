class Hangman {
    field String secret;
    field String hint;
    field int secretLength;
    field Array guessedChars;

    constructor Hangman new(String secretWord){
        let secret = secretWord;
        let secretLength = secret.length();

        do initGuessArray();
        do initHintString();

        return this;
    }

    method void initGuessArray(){
        var int i;

        let guessedChars = Array.new(secretLength);
        let i = 0;

        while (i < secretLength){
            let guessedChars[i] = false;
            let i = i + 1;
        }

        return;
    }

    method void initHintString() {
        var int i;
        var String underscore;
        var char underscoreChar;

        let underscore = "_";
        let underscoreChar = underscore.charAt(0);

        let hint = String.new(secretLength);

        while (i < secretLength) {
            do hint.appendChar(underscoreChar);
            let i = i + 1;
        }

        return;
    }

    method boolean tryCharAndCheckIfMatched(char guess){
        // Returns if guess was correct
        var int i;
        var boolean matched;

        let i = 0;
        let matched = false;

        while (i < secretLength) {
            if (secret.charAt(i) = guess) {
                let guessedChars[i] = true;
                let matched = true;

                do hint.setCharAt(i, guess);
            }
            let i = i + 1;
        }

        return matched;
    }

    method boolean gameWon(){
        var int i;

        let i = 0;

        while (i < secretLength) {
            if (guessedChars[i] = false) {
                return false;
            }
            let i = i + 1;
        }

        return true;
    }

    method String getHint(){
        return hint;
    }
}