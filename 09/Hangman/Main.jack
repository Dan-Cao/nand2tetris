/** Hangman program. */
class Main {
    function void main() {
        var char guess;
        var String hint;
        var int guessesRemaining;
        var Hangman hangman;

        let hangman = Hangman.new("BANANA");
        let guessesRemaining = 10;

        do Output.printString("Enter your guess");
        do Output.println();
        do Output.println();

        while (~(guessesRemaining = 0)) {
            let hint = hangman.makeHintString();
            do Output.printString(hint);
            do hint.dispose();

            if (hangman.gameWon()) {
                do Output.println();
                do Output.println();
                do Output.printString("You won!");
                return;
            }

            do Output.printString(" ");
            do Output.printInt(guessesRemaining);
            do Output.printString(" ");

            let guess = Keyboard.readChar();
            do Output.println();

            if (~(hangman.updateGuessArray(guess))) {
                let guessesRemaining = guessesRemaining - 1;
            }
        }

        do Output.println();
        do Output.printString("Game over! :(");

        return;
    }

}