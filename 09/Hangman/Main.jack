/** Hangman program. */
class Main {
    function void main() {
        var char guess;
        var String secret;
        var String hint;
        var int guessesRemaining;
        var Array guessedChars;

        let secret = "BANANA";
        let guessedChars = Main.makeGuessArray(secret);
        let guessesRemaining = 10;

        do Output.printString("Enter your guess");
        do Output.println();
        do Output.println();

        while (~(guessesRemaining = 0)) {
            let hint = Main.makeHintString(secret, guessedChars);
            do Output.printString(hint);

            if (Main.gameWon(guessedChars, secret)) {
                do Output.println();
                do Output.println();
                do Output.printString("You won!");
                return;
            }

            do Output.printString(" ");
            do Output.printInt(guessesRemaining);
            do Output.printString(" ");

            let guess = Keyboard.readChar();
            do Output.println();

            if (~(Main.updateGuessArray(secret, guess, guessedChars))) {
                let guessesRemaining = guessesRemaining - 1;
            }

            do hint.dispose();
        }

        do Output.println();
        do Output.printString("Game over! :(");

        return;
    }

    function Array makeGuessArray(String secret){
        var Array guessedChars;
        var int length;
        var int i;

        let length = secret.length();
        let guessedChars = Array.new(length);
        let i = 0;

        while (i < length){
            let guessedChars[i] = false;
            let i = i + 1;
        }

        return guessedChars;
    }

    function String makeHintString(String original, Array guessedChars) {
        var String hint;
        var int i;
        var int length;
        var String underscore;
        var char u;

        let underscore = "_";
        let u = underscore.charAt(0);

        let length = original.length();
        let hint = String.new(length);

        while (i < length) {
            if (guessedChars[i] = true) {
                do hint.appendChar(original.charAt(i));
            } else {
                do hint.appendChar(u);
            }
            let i = i + 1;
        }
        return hint;
    }

    function boolean updateGuessArray(String secret, char guess, Array guessedChars){
        var int length;
        var int i;
        var boolean matched;

        let length = secret.length();
        let i = 0;
        let matched = false;

        while (i < length) {
            if (secret.charAt(i) = guess) {
                let guessedChars[i] = true;
                let matched = true;
            }
            let i = i + 1;
        }

        return matched;
    }

    function boolean gameWon(Array guessedChars, String secret){
        var int length;
        var int i;

        let length = secret.length();
        let i = 0;

        while (i < length) {
            if (guessedChars[i] = false) {
                return false;
            }
            let i = i + 1;
        }

        return true;
    }
}